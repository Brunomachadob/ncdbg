plugins {
    // Note: Version 0.12 fits Gradle 2.13. For Gradle 2.14.1, use 0.13. For Gradle 3.0, use 0.14 (or newer).
    id "com.github.maiflai.scalatest" version "0.12"
}

group 'com.programmaticallyspeaking'
version '1.0-SNAPSHOT'

apply plugin: 'scala'
apply plugin: 'application'

def jdkHome = System.getenv("JAVA_HOME")

sourceCompatibility = 1.8

mainClassName = "com.programmaticallyspeaking.ncd.boot.Boot"

// For the application plugin
applicationName = "ncdbg"

test {
    // Always run tests!
    outputs.upToDateWhen {false}

    systemProperty "SelectiveActorLogging.suppress", "true"
    systemProperty "logback.configurationFile", "logback-off.xml"
}

run {
    // Useful for testing command-line argument parsing using "gradle run -Pargs='...'"
    if (project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    def logbackVersion = '1.1.7'
    def akkaHttpVersion = '10.0.3'
    def akkaVersion = '2.4.17'

    // needed for gradle-scalatest
    testRuntime 'org.pegdown:pegdown:1.6.0'

    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.1'
    testCompile group: 'org.scalacheck', name: 'scalacheck_2.12', version: '1.13.4'

    testCompile group: 'com.typesafe.akka', name: 'akka-http-testkit_2.12', version: akkaHttpVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.27'

    compile files("$jdkHome/lib/tools.jar")

    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

    // TODO: Update this once a proper slf4s-api release is available.
    compile 'com.github.provegard:slf4s:2cbc154cf095d9e2027bf978502bb7443afcfdf3'
//    compile group: 'org.slf4s', name: 'slf4s-api_2.12', version: '1.7.13'

    compile group: 'com.typesafe.akka', name: 'akka-actor_2.12', version: akkaVersion
    compile group: 'com.typesafe.akka', name: 'akka-http_2.12', version: akkaHttpVersion

    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.12', version: '2.8.4'

    compile 'org.scala-lang:scala-library:2.12.1'

    compile group: 'org.rogach', name: 'scallop_2.12', version: '2.1.0'
}
